[[sec_composite_combining_maps]]
=== Combining Maps
[role="byline"]
by Tom Hicks

// TODO: Rebalance recipe and/or integrate Luke's merge discussion:

// Another common way to update a map is to merge it with another
// one. Clojure supports this with the +merge+ function, which takes any
// number of maps, and returns a map containing all the keys and values
// in a single map. The maps are merged in argument order from left to
// right, so if a key is present in more than one of the maps, the value
// used will be the one in the final map to contain the key.
//
// [source,clojure]
// ----
// (def public {:name "Bruce Wayne", :occupation "Playboy"})
// (def secret {:training "Ninja" :occupation "Vigilante"})
// (def batman (merge public secret))
// ;; -> {:name "Bruce Wayne" :occupation "Vigilante" :training "Ninja"}
// ----
//
// Sometimes, when merging maps which have the same key, it is possible
// to be smarter about combining them instead of doing a simple overwrite
// as +merge+ does. The +merge-with+ function allows you to specify a
// two-argument function that will be called with both values, with the
// result used as the final value in the resulting map.
//
// For example, the following example merges two maps, and if a key is
// the same, it uses the +`+`+ function to add the values together.
//
// [source,clojure]
// ----
// (def print-sales {2010 153, 2011 206, 2012 321})
// (def ebook-sales {2011 25, 2012 83, 2013 104})
// (def total-sales (merge-with + print-sales ebook-sales))
// ;; -> {2010 153, 2011 231, 2012 404, 2013 104}
// ----

==== Problem

You have two or more maps you wish to combine to produce a single map.

==== Solution

Use +merge+ to combine two or more maps with no keys in common.

[source,clojure]
----
(def arizona-bird-counts {:cactus-wren 8})
(def florida-bird-counts {:gull 20 :pelican 14})

(merge florida-bird-counts arizona-bird-counts)
;; -> {:pelican 14, :cactus-wren 8, :gull 20}
----

When mappings for the same key exist in more than one map, the latter
mapping is used in the result.

// TODO: This example is a little hard to read

[source,clojure]
----
(def vote-count-AM   {"W.H.Harrison" 82, "J.K.Polk" 54, "M.Fillmore" 12, "U.S.Grant" 727})
(def vote-count-NOON {"Z.Taylor" 77, "W.H.Harrison" 441, "J.K.Polk" 254})
(def vote-count-PM   {"W.H.Harrison" 2360, "J.K.Polk" 554, "M.Fillmore" 1312})

(merge vote-count-AM vote-count-NOON vote-count-PM)
;; -> {"J.K.Polk" 554, "U.S.Grant" 727, "Z.Taylor" 77,
;;     "M.Fillmore" 1312, "W.H.Harrison" 2360}
----

Use +merge-with+ when you want to build up a resulting value for keys
that exist in more than one map.

[source,clojure]
----
(def florida-bird-counts {:gull 20 :anhinga 8 :pelican 14 :egret 4 })
(def california-bird-counts {:gull 12 :pelican 4 :egret 11 :jay 3})

(merge-with + california-bird-counts florida-bird-counts)
;; -> {:pelican 18, :egret 15, :gull 32, :jay 3, :anhinga 8}
----

With careful choice of map value types, +merge-with+ provides some
concise solutions to common problems. In this example, we find shared
likes and dislikes among our programming team using sets:

[source,clojure]
----
(def Alice {:loves #{:clojure :lisp :scheme} :hates #{:fortran :c :c++}})
(def Bob   {:loves #{:clojure :scheme} :hates #{:c :c++ :algol}})
(def Ted   {:loves #{:clojure :lisp :scheme} :hates #{:algol :basic :c :c++ :fortran}})

(merge-with clojure.set/intersection Alice Bob Ted)
;; -> {:loves #{:scheme :clojure}, :hates #{:c :c++}}
----

Suppose we have a collection of recent scores for our bowling team.
Each game is represented by a map of scores, keyed by bowler. We can
use +merge-with+ to create a single map which records all scores
earned by all bowlers:

[source,clojure]
----
(def scores [{:bob 240 :ted 200 :carol 280 :alice 130}
             {:don 200 :ted 180 :carol 240 :alice 100}
             {:bob 200 :ted 180 :carol 240 :alice 100}
             {:bob 200 :ted 180 :carol 240 :mary 100 :don 144}])

(apply merge-with (comp flatten vector) scores)
;; -> {:don (200 144), :ted (200 180 180 180), :bob (240 200 200),
;;     :carol (280 240 240 240), :mary 100, :alice (130 100 100)}
----

You can even merge nested maps by recursively merging duplicate keys.

[source,clojure]
----
(defn deep-merge
  [& maps]
  (apply merge-with deep-merge maps))
----

==== Discussion

Both +merge+ and +merge-with+ combine their argument maps from left to
right, returning a new immutable map as a result. In the simple case
of +merge+ the rightmost map "wins" for any common keys.

+merge-with+ facilitates powerful recipes for map combination by
taking an additional function argument. You can imagine +merge-with+
as +reduce+ for maps with common keys. The supplied merging function
must take two arguments, and is called with the values for each pair
of duplicate keys as the result map is computed. Mappings for unique
keys remain unchanged in the result.

==== See also

* See <<sec_composite_data_maps_setting_keys>> to learn more about
  manipulating maps.
* See <<sec_composite_data_maps_multiple_values>> for information on
  creating multiple value maps using a Clojure protocol.
